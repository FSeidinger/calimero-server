buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.3.1'

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'application'

group = 'com.github.calimero'
version = '2.5-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

mainClassName = 'tuwien.auto.calimero.server.Launcher'

run {
    // for passing program arguments, use -Dexec.args="..."
    args System.getProperty("exec.args", "").split() 
    standardInput = System.in
}

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

test {
	useJUnitPlatform()
}

dependencies {
    api "com.github.calimero:calimero-core:${version}"
    api "com.github.calimero:calimero-device:${version}"
    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
    runtimeOnly 'org.slf4j:slf4j-simple:1.8.0-beta4'

	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
	testCompileOnly 'junit:junit:4.12'
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
	
	// knx secure
	implementation 'org.bouncycastle:bcprov-jdk15on:1.60'
}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-SNAPSHOT'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.0.0-SNAPSHOT'
ext.junitPlatformVersion = '1.0.0-SNAPSHOT'

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.github.calimero'
version = '2.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'tuwien.auto.calimero.server.Launcher'

run {
    // for passing program arguments, use -Dexec.args="..."
    args System.getProperty("exec.args", "").split() 
    standardInput = System.in
}

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDirs = [ 'test' ]
        file('build/classes/test').mkdirs()
        runtimeClasspath = runtimeClasspath + files('build/classes/test')
    }
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

junitPlatform {
	details 'none' // 'tree'
}

dependencies {
    api "com.github.calimero:calimero-core:${version}"
    api "com.github.calimero:calimero-device:${version}"
    implementation 'org.slf4j:slf4j-api:1.7.22'
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.22'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"
    testRuntimeOnly 'junit:junit:4.12'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:4.12.0-SNAPSHOT'
}
